apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    mavenCentral()
    mavenRepo urls: "http://m2repo.spockframework.org/snapshots"
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy-all', version: '1.7.3'
    compile 'net.sf.opencsv:opencsv:2.1'
    testCompile "org.spockframework:spock-core:0.4-groovy-1.7"
    testCompile "cglib:cglib-nodep:2.2"
    testCompile "org.objenesis:objenesis:1.2"
}

version = '0.2-SNAPSHOT'
group = 'com.xlson.groovycsv'
packaging = 'jar'

sourceSets {
    main {
        groovy {
            srcDir 'src'
        }
    }
    test {
        groovy {
            srcDir 'test'
        }
    }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives groovydocJar
    archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        name 'GroovyCSV'
        description 'Library for parsing csv in Groovy'
        url 'http://github.com/xlson/groovycsv'
        inceptionYear '2010'
        
        scm {
            url 'http://github.com/xlson/groovycsv'
            connection 'http://github.com/xlson/groovycsv'
        }
        
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}

task downloadDeps << {
  def libDir = file('lib')
  ant.delete(dir: libDir)
  copy {
    from configurations.testRuntime
    into libDir
  }
}
